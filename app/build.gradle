apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"
    defaultConfig {
        applicationId "net.egordmitriev.cheatsheets"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode Integer.parseInt(project.VERSION_MAJOR) * 10000 + Integer.parseInt(project.VERSION_MINOR) * 100 + Integer.parseInt(project.VERSION_PATCH)
        versionName "${project.VERSION_MAJOR}.${project.VERSION_MINOR}.${project.VERSION_PATCH}"
        project.ext.set("archivesBaseName", "CheatSheets-" + defaultConfig.versionName);
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resConfigs "en"
    }
    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'mips', 'mips64'
            universalApk false
        }
    }
    buildTypes {
        release {
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }


}

project.ext.versionCodes = ['armeabi': 1, 'armeabi-v7a': 2, 'arm64-v8a': 3, 'mips': 5, 'mips64': 6, 'x86': 8, 'x86_64': 9]

android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        output.versionCodeOverride =
                project.ext.versionCodes.get(output.getFilter(
                        com.android.build.OutputFile.ABI), 0) * 10000000 + android.defaultConfig.versionCode
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:26.+'
    compile 'com.android.support:design:26.+'
    compile 'com.android.support:cardview-v7:26.+'
    compile 'com.android.support:recyclerview-v7:26.+'

    compile 'com.balysv.materialmenu:material-menu:2.0.0'
    compile 'net.cachapa.expandablelayout:expandablelayout:2.9.1'
    compile 'net.egordmitriev.loaderview:loaderview:1.3'
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    //    compile 'com.github.barteksc:android-pdf-viewer:2.7.0-beta.1'
    compile 'com.orhanobut:logger:2.1.1'
    compile 'com.jakewharton:butterknife:8.6.0'

    compile(name:'libmupdf-1.1', ext:'aar')

    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'
    testCompile 'junit:junit:4.12'

}